type classesType = {
  basicsDescriptionList: string,
  root: string,
  scrollContent: string,
  unfinishedConcumptionsTable: string,
}
@module("./PersonDetail.module.css") external classes: classesType = "default"

type unfinishedConsumptionsRecord = {
  kegId: string,
  beer: string,
  milliliters: int,
  createdAt: Js.Date.t,
}

@react.component
let make = (
  ~onDismiss,
  ~onDeleteConsumption,
  ~onNextPerson,
  ~onPreviousPerson,
  ~person: Db.personsAllRecord,
  ~personId,
  ~placeId,
  ~unfinishedConsumptions: array<unfinishedConsumptionsRecord>,
) => {
  let {data: maybePersonDoc} = Db.usePlacePersonDocumentStatus(
    ~options={suspense: false},
    placeId,
    personId,
  )
  <Dialog className={classes.root} visible={true}>
    <header>
      <h3> {React.string(person.name)} </h3>
      <button className={`${Styles.buttonClasses.button}`} onClick=onPreviousPerson type_="button">
        {React.string("‚¨ÖÔ∏è P≈ôedchoz√≠")}
      </button>
      <button className={`${Styles.buttonClasses.button}`} onClick=onNextPerson type_="button">
        {React.string("N√°sleduj√≠c√≠ ‚û°Ô∏è")}
      </button>
    </header>
    <div className={classes.scrollContent}>
      <section ariaLabel="Z√°kladn√≠ √∫daje">
        <dl className={`reset ${classes.basicsDescriptionList}`}>
          <div>
            <dt> {React.string("ji≈æ od")} </dt>
            <dd>
              {switch maybePersonDoc {
              | None => React.string("naƒç√≠t√°m‚Ä¶")
              | Some(personDoc) =>
                <FormattedDateTime value={personDoc.createdAt->Firebase.Timestamp.toDate} />
              }}
            </dd>
          </div>
          <div>
            <dt> {React.string("naposledy")} </dt>
            <dd>
              <FormattedDateTime value={person.recentActivityAt->Firebase.Timestamp.toDate} />
            </dd>
          </div>
          <div>
            <dt> {React.string("stav konta")} </dt>
            <dd>
              {switch maybePersonDoc {
              | None => React.string("naƒç√≠t√°m‚Ä¶")
              | Some(personDoc) =>
                <FormattedCurrency
                  format={FormattedCurrency.formatAccounting} value=personDoc.balance
                />
              }}
            </dd>
          </div>
        </dl>
      </section>
      <section ariaLabelledby="unfinished_consumptions">
        {unfinishedConsumptions->Array.length === 0
          ? <p> {React.string("Pr√°zdn√©")} </p>
          : <table className={classes.unfinishedConcumptionsTable}>
              <caption> {React.string("Neza√∫ƒçtovan√° piva")} </caption>
              <thead>
                <tr>
                  <th scope="col"> {React.string("Pivo")} </th>
                  <th scope="col"> {React.string("Objem")} </th>
                  <th scope="col"> {React.string("Kdy")} </th>
                  <th scope="col">
                    <span className={Styles.utilityClasses.srOnly}> {React.string("Akce")} </span>
                  </th>
                </tr>
              </thead>
              <tbody>
                {unfinishedConsumptions
                ->Array.map(consumption => {
                  let createdAt = consumption.createdAt->Js.Date.toISOString
                  <tr key={createdAt}>
                    <td> {React.string(consumption.beer)} </td>
                    <td>
                      {React.string(
                        `${(consumption.milliliters->Int.toFloat /. 1000.0)
                            ->Float.toFixedWithPrecision(~digits=1)} L`,
                      )}
                    </td>
                    <td>
                      <FormattedDateTime value=consumption.createdAt />
                    </td>
                    <td>
                      <button
                        className={`${Styles.buttonClasses.button}`}
                        onClick={_ => onDeleteConsumption(consumption)}
                        type_="button">
                        {React.string("üóëÔ∏è Sm√°znout")}
                      </button>
                    </td>
                  </tr>
                })
                ->React.array}
              </tbody>
            </table>}
      </section>
    </div>
    <footer>
      <button className={`${Styles.buttonClasses.button}`} onClick={onDismiss}>
        {React.string("Zav≈ô√≠t")}
      </button>
    </footer>
  </Dialog>
}
