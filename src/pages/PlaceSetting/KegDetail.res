type classesType = {root: string}
@module("./KegDetail.module.css") external classes: classesType = "default"

type dialogState = Hidden | ConfirmDelete | ConfirmFinalize

type dialogEvent = Hide | ShowConfirmDelete | ShowConfirmFinalize

let dialogReducer = (_, event) => {
  switch event {
  | Hide => Hidden
  | ShowConfirmDelete => ConfirmDelete
  | ShowConfirmFinalize => ConfirmFinalize
  }
}

@react.component
let make = (
  ~hasNext,
  ~hasPrevious,
  ~keg: Db.kegConverted,
  ~place: Db.placeConverted,
  ~onDeleteConsumption,
  ~onDeleteKeg,
  ~onDismiss,
  ~onFinalizeKeg,
  ~onPreviousKeg,
  ~onNextKeg,
) => {
  let consumptionsByTimestampDesc = React.useMemo1(() =>
    keg.consumptions
    ->Belt.Map.String.toArray
    // The map is sorted by timestamp ascending, we want descending
    ->Array.reverse
  , [keg.consumptions])
  let firstConsumption =
    consumptionsByTimestampDesc
    ->Array.get(consumptionsByTimestampDesc->Array.length - 1)
    ->Option.flatMap(((timestampStr, _)) => timestampStr->Float.fromString)
  let priceLargeBeer =
    (keg.price->Int.toFloat /. keg.milliliters->Int.toFloat *. 500.0)->Int.fromFloat
  let effectivity = keg.consumptionsSum->Int.toFloat /. keg.milliliters->Int.toFloat
  let kegId = Db.getUid(keg)->Option.getExn
  let kegName = `${keg.serialFormatted} ${keg.beer}`
  let maybeTapName =
    place.taps
    ->Belt.Map.String.findFirstBy((_, maybeKegRef) =>
      maybeKegRef
      ->Null.toOption
      ->Option.map(kegRef => kegRef.id === kegId)
      ->Option.getWithDefault(false)
    )
    ->Option.map(((tapName, _)) => tapName)
  let (dialogState, sendDialog) = React.useReducer(dialogReducer, Hidden)
  let hideDialog = _ => sendDialog(Hide)
  <DialogCycling
    hasNext
    hasPrevious
    className=classes.root
    header={kegName}
    onDismiss
    onNext=onNextKeg
    onPrevious=onPreviousKeg
    visible=true>
    <dl className={`reset ${Styles.descriptionListClasses.inline}`}>
      <div>
        <dt> {React.string("objem")} </dt>
        <dd>
          <FormattedVolume milliliters={keg.milliliters} />
        </dd>
      </div>
      <div>
        <dt> {React.string("naskladnƒõno dne")} </dt>
        <dd>
          <FormattedDateTime value={keg.createdAt->Firebase.Timestamp.toDate} />
        </dd>
      </div>
      <div>
        <dt> {React.string("za")} </dt>
        <dd>
          <FormattedCurrency value={keg.price} />
        </dd>
      </div>
      <div>
        <dt> {React.string("t.j. velk√© pivo za")} </dt>
        <dd>
          <FormattedCurrency value={priceLargeBeer} />
        </dd>
      </div>
      {switch firstConsumption {
      | None => React.null
      | Some(timestamp) =>
        <div>
          <dt> {React.string("prvn√≠ v√Ωtoƒç")} </dt>
          <dd>
            <FormattedDateTime value={timestamp->Js.Date.fromFloat} />
          </dd>
        </div>
      }}
      {switch keg.depletedAt->Null.toOption {
      | None => React.null
      | Some(depletedAt) =>
        <>
          <div>
            <dt> {React.string("dopito")} </dt>
            <dd>
              <FormattedDateTime value={depletedAt->Firebase.Timestamp.toDate} />
            </dd>
          </div>
          <div>
            <dt> {React.string("ze sudu se vytoƒçilo")} </dt>
            <dd>
              <FormattedVolume milliliters={keg.milliliters} />
            </dd>
          </div>
          <div>
            <dt> {React.string("efektivita")} </dt>
            <dd>
              <FormattedPercent value={effectivity *. 100.0} />
            </dd>
          </div>
          <div>
            <dt> {React.string("v√Ωsledn√° cena za velk√© pivo")} </dt>
            <dd>
              <FormattedCurrency
                value={(priceLargeBeer->Int.toFloat /. effectivity)->Int.fromFloat}
              />
            </dd>
          </div>
        </>
      }}
    </dl>
    {switch maybeTapName {
    | None => React.null
    | Some(tapName) =>
      <p className={Styles.messageBarClasses.info}>
        {React.string(`Sud je nara≈æen na p√≠pu ${tapName}`)}
      </p>
    }}
    {switch consumptionsByTimestampDesc {
    | [] =>
      <p>
        {React.string("Ze sudu zat√≠m neevidujeme ƒçepov√°n√≠.")}
        {switch maybeTapName {
        | Some(_) => React.null
        | None =>
          <>
            {React.string(" Pokud jste sud p≈ôidali omylem m≈Ø≈æete ho ")}
            <button
              className={Styles.linkClasses.base}
              onClick={_ => sendDialog(ShowConfirmDelete)}
              type_="button">
              {React.string("odebrat z aplikace")}
            </button>
            {React.string(".")}
          </>
        }}
      </p>
    | _ =>
      <>
        <table className={Styles.tableClasses.consumptions}>
          <caption> {React.string("Natoƒçen√° piva")} </caption>
          <thead>
            <tr>
              <th scope="col"> {React.string("Jm√©no")} </th>
              <th scope="col"> {React.string("Objem")} </th>
              <th scope="col"> {React.string("Kdy")} </th>
              <th scope="col">
                <span className={Styles.utilityClasses.srOnly}> {React.string("Akce")} </span>
              </th>
            </tr>
          </thead>
          <tbody>
            {keg.consumptions
            ->Belt.Map.String.toArray
            // The map is sorted by timestamp ascending, we want descending
            ->Array.reverse
            ->Array.map(((timestampStr, consumption)) => {
              let person = place.personsAll->Belt.Map.String.getExn(consumption.person.id)
              let createdData = timestampStr->Float.fromString->Option.getExn->Js.Date.fromFloat
              <tr key={timestampStr}>
                <td> {React.string(person.name)} </td>
                <td>
                  <FormattedVolume milliliters=consumption.milliliters />
                </td>
                <td>
                  <FormattedDateTime value={createdData} />
                </td>
                <td>
                  {keg.depletedAt !== Null.null
                    ? React.null
                    : <button
                        className={`${Styles.buttonClasses.button}`}
                        onClick={_ => onDeleteConsumption(timestampStr)}
                        type_="button">
                        {React.string("üóëÔ∏è Sm√°znout")}
                      </button>}
                </td>
              </tr>
            })
            ->React.array}
          </tbody>
        </table>
        {keg.depletedAt !== Null.null
          ? React.null
          : <button
              className={`${Styles.buttonClasses.button} ${Styles.buttonClasses.variantDanger}`}
              onClick={_ => sendDialog(ShowConfirmFinalize)}
              type_="button">
              {React.string("Odepsat ze skladu a roz√∫ƒçtovat")}
            </button>}
      </>
    }}
    {switch dialogState {
    | Hidden => React.null
    | ConfirmDelete =>
      <DialogConfirmation
        className={DialogConfirmation.classes.deleteConfirmation}
        heading="Odstranit sud ?"
        onConfirm={() => {
          onDismiss()
          onDeleteKeg()
        }}
        onDismiss={() => hideDialog()}
        visible=true>
        <p>
          {React.string(`Chyst√°te se odstranit sud `)}
          <b> {React.string(kegName)} </b>
          {React.string(` z aplikace. Chcete pokraƒçovat?`)}
        </p>
      </DialogConfirmation>
    | ConfirmFinalize =>
      <DialogConfirmation
        className={DialogConfirmation.classes.deleteConfirmation}
        heading="Chyst√°te se roz√∫ƒçtovat sud"
        onConfirm={() => {
          onDismiss()
          onFinalizeKeg()
        }}
        onDismiss={() => hideDialog()}
        visible=true>
        <dl>
          <dt> {React.string("N√°zev sudu")} </dt>
          <dd> {React.string(kegName)} </dd>
          <dt> {React.string("Celkem vytoƒçeno")} </dt>
          <dd>
            <FormattedVolume milliliters=keg.consumptionsSum />
            {React.string(" z ")}
            <FormattedVolume milliliters=keg.milliliters />
            {React.string(" (efektivita ")}
            <FormattedPercent value={effectivity *. 100.0} />
            {React.string(")")}
          </dd>
          <dt> {React.string(`V√Ωsledn√° cena velk√©ho piva${HtmlEntities.nbsp}*`)} </dt>
          <dd>
            <FormattedCurrency
              value={(priceLargeBeer->Int.toFloat /. effectivity)->Int.fromFloat}
            />
          </dd>
        </dl>
        <p>
          {React.string(`* Mezi konzumenty se rozpoƒç√≠t√°v√° cena sudu, v√Ωslednou cenu velk√©ho piva tak ovliv≈àuje
          efektivita v√Ωtoƒçe.`)}
        </p>
      </DialogConfirmation>
    }}
  </DialogCycling>
}
